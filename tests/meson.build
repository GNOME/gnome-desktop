test_args = [
  '-DG_LOG_DOMAIN="GnomeDesktop"',
  '-DGDK_DISABLE_DEPRECATED_WARNINGS',
  '-DGTK_DISABLE_DEPRECATED_WARNINGS',
  '-DINSTALLED_TEST_DIR="@0@"'.format(test_execdir)
]

test_programs = [
  'wall-clock',
 'wallclock-reftest'
]

test_metas = []

foreach program: test_programs
  exe = executable(program, program + '.c',
    dependencies: libgnome_desktop_dep,
    c_args: test_args,
    install_dir: test_execdir,
    install: get_option('installed_tests')
  )

  if get_option('installed_tests')
    meta_data = configuration_data()
    meta_data.set('TEST_EXEC', join_paths(test_execdir, program))

    test_metas += configure_file(
      configuration: meta_data,
      input: 'meta.test.in',
      output: program + '.test',
    )
  endif
endforeach

if get_option('installed_tests')
  test_data = files(
    'C.ref.ui',
    'C.ui',
    'en_US.utf-8.ref.ui',
    'en_US.utf-8.ui',
    'he_IL.utf8.ref.ui',
    'he_IL.utf8.ui'
  )
  install_data(test_data + test_metas, install_dir: test_metadir)
endif
