AC_INIT(libgnome-desktop)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(gnome-desktop, 2.0.3)

AM_MAINTAINER_MODE

AC_PROG_INTLTOOL

AC_ISC_POSIX
AC_PROG_CC
AC_STDC_HEADERS
AC_ARG_PROGRAM
AM_PROG_LIBTOOL

GNOME_COMPILE_WARNINGS(yes)

GNOME_PLATFORM=2
GNOME_MINOR=0
GNOME_MICRO=0
GNOME_VENDOR=""
GNOME_DATE=""
GNOME_DESCRIPTION=""

AC_SUBST(GNOME_PLATFORM)
AC_SUBST(GNOME_MINOR)
AC_SUBST(GNOME_MICRO)
AC_SUBST(GNOME_VENDOR)
AC_SUBST(GNOME_DATE)
AC_SUBST(GNOME_DESCRIPTION)

dnl If you add a version number here, you *must* add an AC_SUBST line for
dnl it too, or it will never make it into the spec file!

GDK_PIXBUF_REQUIRED=2.0.3
GTK_REQUIRED=2.0.3
LIBGNOMECANVAS_REQUIRED=2.0.0
LIBGNOMEUI_REQUIRED=2.0.0
GNOME_VFS_MODULE_REQUIRED=1.9.16

AC_SUBST(GTK_REQUIRED)
AC_SUBST(GDK_PIXBUF_REQUIRED)
AC_SUBST(LIBGNOMEUI_REQUIRED)
AC_SUBST(LIBGNOMECANVAS_REQUIRED)
AC_SUBST(GNOME_VFS_MODULE_REQUIRED)

dnl pkg-config dependency checks

PKG_CHECK_MODULES(GNOME_DESKTOP, gdk-pixbuf-2.0 >= $GDK_PIXBUF_REQUIRED gtk+-2.0 >= $GTK_REQUIRED libgnomeui-2.0 >= $LIBGNOMEUI_REQUIRED gnome-vfs-module-2.0 >= $GNOME_VFS_MODULE_REQUIRED)
AC_SUBST(GNOME_DESKTOP_CFLAGS)
AC_SUBST(GNOME_DESKTOP_LIBS)

PKG_CHECK_MODULES(GNOME_ABOUT, gdk-pixbuf-2.0 >= $GDK_PIXBUF_REQUIRED gtk+-2.0 >= $GTK_REQUIRED libgnomeui-2.0 >= $LIBGNOMEUI_REQUIRED libgnomecanvas-2.0 >= $LIBGNOMECANVAS_REQUIRED)
AC_SUBST(GNOME_ABOUT_CFLAGS)
AC_SUBST(GNOME_ABOUT_LIBS)


dnl Language Support

GETTEXT_PACKAGE=gnome-desktop-2.0
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE")
AC_SUBST(GETTEXT_PACKAGE)

ALL_LINGUAS="ar az bg ca cs da de el en_GB es et eu fi fr ga gl hi hu it ja ko lt lv mk ms nl nn no pl pt pt_BR ro ru sk sl sp sr sv ta tr uk vi wa zh_CN zh_TW"
AM_GLIB_GNU_GETTEXT


dnl Place to look for KDE menus

AC_ARG_WITH(kde-datadir, [  --with-kde-datadir=DIR  Location of KDE data])

if test "x$with_kde_datadir" = x ; then
  kde_datadir=/usr/share
else
  kde_datadir=$with_kde_datadir
fi

AC_DEFINE_UNQUOTED(KDE_MENUDIR, "$kde_datadir/applnk")
AC_DEFINE_UNQUOTED(KDE_ICONDIR, "$kde_datadir/icons")
AC_DEFINE_UNQUOTED(KDE_MINI_ICONDIR, "$kde_datadir/icons/mini")


ICONDIR=`eval echo "${datadir}/pixmaps"`
AC_DEFINE_UNQUOTED(GNOME_ICONDIR,"${ICONDIR}")

dnl Build Flags 

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

dnl Don't use AC_PROG_AWK since we need the full pathname.
AC_PATH_PROGS(AWK, mawk gawk nawk awk, )
AC_PATH_PROGS(PERL, perl5 perl)

dnl define a MAINT-like variable REBUILD which is set if Perl
dnl and awk are found, so autogenerated sources can be rebuilt
AC_ARG_ENABLE(rebuilds, [  --disable-rebuilds      disable all source autogeneration rules],,enable_rebuilds=yes)
REBUILD=\#
if test "x$enable_rebuilds" = "xyes" && \
     test -n "$PERL" && \
     $PERL -e 'exit !($] >= 5.002)' > /dev/null 2>&1 && \
     test -n "$AWK" ; then
  REBUILD=
fi
AC_SUBST(REBUILD)

AC_OUTPUT([
Makefile
gnome-desktop.spec
desktop-links/Makefile
gnome-about/Makefile
libgnome-desktop/Makefile
libgnome-desktop/gnome-desktop-2.0.pc
gnome-version.xml
po/Makefile.in
pixmaps/Makefile
desktop-docs/Makefile
desktop-docs/fdl/Makefile
desktop-docs/fdl/C/Makefile
desktop-docs/gpl/Makefile
desktop-docs/gpl/C/Makefile
desktop-docs/lgpl/Makefile
desktop-docs/lgpl/C/Makefile
man/Makefile
])
